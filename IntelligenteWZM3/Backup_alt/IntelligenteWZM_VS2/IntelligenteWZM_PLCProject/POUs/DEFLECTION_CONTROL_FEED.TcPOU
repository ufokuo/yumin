<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.26">
  <POU Name="DEFLECTION_CONTROL_FEED" Id="{6d7ce70a-5ddf-4992-a4f2-b73a7bfe4091}">
    <Declaration><![CDATA[PROGRAM DEFLECTION_CONTROL_FEED
VAR
		USED_FORCE_SIGNAL: REAL:=0;
		DEFLECTION_REAL: REAL:=0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF ACT_NC_PROGRAM=5 THEN
	
	//USED_FORCE_SIGNAL:=-A_SIGNAL_2;
	//USED_FORCE_SIGNAL:=SIGNAL_FORCE_FLT[2];
	USED_FORCE_SIGNAL:=SQRT(EXPT(A_SIGNAL_1,2)+EXPT(A_SIGNAL_2,2));
		
	IF USED_FORCE_SIGNAL<0 THEN
		FORCE_ACT:=0;
		ELSE FORCE_ACT:=USED_FORCE_SIGNAL;	
	END_IF
	
	
	TOOL_STIFFNESS_ACT:=TOOL_STIFFNESS_SELC_PTS[1,3];
	//TOOL_STIFFNESS_ACT:=TOOL_STIFFNESS_SELC_PTS[2,3];
	DEFLECTION_ACT:=FORCE_ACT/TOOL_STIFFNESS_ACT;		//µm
		
	
	IF ACT_NC_PROGRAM_STEP=1 THEN
			DEFLECTION_CONTROL_ON:=1;			//DEFLECTION CONTROL ON		
		ELSE
			DEFLECTION_CONTROL_ON:=0;
	END_IF

	IF DEFLECTION_CONTROL_ON=0 THEN
		CONTROL_RESET:=1;
	END_IF

	IF DEFLECTION_CONTROL_ON=1 AND CONTROL_RESET=1 THEN
		DEFLECTION_DIF_SUM:=0;
		CUTTING_ON:=0;
		REF_REACHED:=0;
		CONTROL_RESET:=0;
	END_IF

	CASE ACT_NC_PARAM_3 OF					// CONTROL STRUCTURE SELECT 
		1:
			CONTROLLER_TYPE:=1;				// P CONTROLLER
		2:
			CONTROLLER_TYPE:=2;				// PI CONTROLLER 
		3:
			CONTROLLER_TYPE:=3;				// PI CONTROLLER WITH ANTI-WINDUP
		ELSE
			CONTROLLER_TYPE:=1;				// P CONTROLLER
	END_CASE

	IF FORCE_ACT>=CUTTING_FORCE_MIN AND CUTTING_ON=0 THEN
		CUTTING_ON:=1;
	END_IF
	
	IF DEFLECTION_ACT>=DEFLECTION_REF AND REF_REACHED=0 THEN
		REF_REACHED:=1;
	END_IF

	CASE ACT_NC_PARAM_4 OF					// START CONDITION SELECTION FOR CONTROLLER
		1:
			CONTROL_START:=1;
			CONTROL_START_CONDITION:=(DEFLECTION_CONTROL_ON=1);				        //CONDITION CONTROL ON
		2:
			CONTROL_START:=2;
			CONTROL_START_CONDITION:=(DEFLECTION_CONTROL_ON=1 AND CUTTING_ON=1);	//CONDITION CUTTING ON
		3:
			CONTROL_START:=3;
			CONTROL_START_CONDITION:=(DEFLECTION_CONTROL_ON=1 AND REF_REACHED=1);	//CONDITION REF REACHED
		ELSE
			CONTROL_START:=1;
			CONTROL_START_CONDITION:=(DEFLECTION_CONTROL_ON=1);
	END_CASE

	IF CONTROL_START_CONDITION=1 THEN
	
		DEFLECTION_DIF:=DEFLECTION_REF-DEFLECTION_ACT;
	
		IF CONTROLLER_TYPE=1 THEN
			OVRD_REG_P:=K_P*DEFLECTION_DIF;
			OVRD_REG_I:=0;
		END_IF
	
		IF CONTROLLER_TYPE=2 THEN
			DEFLECTION_DIF_SUM:=DEFLECTION_DIF_SUM+DEFLECTION_DIF;
			OVRD_REG_P:=K_P*DEFLECTION_DIF;
			OVRD_REG_I:=K_I*T_S*DEFLECTION_DIF_SUM;
		END_IF
	
		IF CONTROLLER_TYPE=3 THEN
			OVRD_DIF:=OVRD_REG-OVRD_MACHINE;
			WINDUP:=K_WINDUP*OVRD_DIF;
			DEFLECTION_DIF_SUM:=DEFLECTION_DIF_SUM+K_I*T_S*DEFLECTION_DIF-WINDUP;
			OVRD_REG_P:=K_P*DEFLECTION_DIF;
			OVRD_REG_I:=DEFLECTION_DIF_SUM;	
		END_IF
		
		OVRD_REG:=OVRD_REG_P+OVRD_REG_I+OVRD_MID;
		OVRD_MACHINE:=OVRD_REG;
	
		IF OVRD_MACHINE > SATURATION_OVRD_MAX THEN
			OVRD_MACHINE:=SATURATION_OVRD_MAX;
		END_IF

		IF OVRD_MACHINE < SATURATION_OVRD_MIN THEN
			OVRD_MACHINE:=SATURATION_OVRD_MIN;
		END_IF
	
		ELSE OVRD_MACHINE:=OVRD_MID;
	
	END_IF
		
	SET_NC_PARAM_2:= REAL_TO_WORD(OVRD_MACHINE);
	ELSE
		SET_NC_PARAM_2:= 100;
END_IF]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>